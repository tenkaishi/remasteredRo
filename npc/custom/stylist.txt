//===== Hercules Script ======================================
//= Stylist
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= Changes your hair style, hair color, and cloth color.
//===== Additional Comments: =================================
//= 1.1 Switched to 'getbattleflag', credits to Saithis. [Euphy]
//============================================================

prontera,172,183,1	script	Styliste#custom_stylist	2_M_DYEINGER,{

	setarray .@styles[1],getbattleflag("max_cloth_color"),getbattleflag("max_hair_style"),getbattleflag("max_hair_color");
	setarray .@Look[1],7,1,6;
	set .@s, select(" ~ Couleur des habits", " ~ Coupe de cheveux", " ~ Couleur des cheveux");
	set .@Revert, getlook(.@Look[.@s]);
	set .@style,1;
	while(1) {
		setlook .@Look[.@s], .@style;
		message strcharinfo(PC_NAME),"C'est le style #"+.@style+".";
		set .@menu$, " ~ Prochain (^0055FF"+((.@style!=.@styles[.@s])?.@style+1:1)+"^000000): ~ Precedent (^0055FF"+((.@style!=1)?.@style-1:.@styles[.@s])+"^000000): ~ Aller au ...: ~ Revenir a l'original (^0055FF"+.@Revert+"^000000)";
		switch(select(.@menu$)) {
			case 1: set .@style, ((.@style!=.@styles[.@s])?.@style+1:1); break;
			case 2: set .@style, ((.@style!=1)?.@style-1:.@styles[.@s]); break;
			case 3: message strcharinfo(PC_NAME),"Choisis un style entre 1 - "+.@styles[.@s]+".";
				input .@style,0,.@styles[.@s];
				if (!.@style) set .@style, rand(1,.@styles[.@s]);
				break;
			case 4: set .@style, .@Revert; setlook .@Look[.@s], .@Revert; break;
		}
	}
}
