//===== Hercules Script ======================================
//= Reset NPC
//===== By: ==================================================
//= Hercules Dev Team
//===== Current Version: =====================================
//= 1.3
//===== Description: =========================================
//= Resets skills, stats, or both.
//===== Additional Comments: =================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//============================================================

prontera,140,180,5	script	Resetteur	4_M_RUPERT,{

	set .@ResetStat,5000;	// Zeny for stat reset
	set .@ResetSkill,5000;	// Zeny for skill reset
	set .@ResetBoth,9000;	// Zeny for resetting both together

	mes "[Resetteur]";
	mes "Bonjour.";
	mes "Reset Stats : "+ .@ResetStat +"z";
	mes "Reset Skills : "+ .@ResetSkill +"z";
	mes "Reset les 2 : "+ .@ResetBoth +"z";
	mes "Choisis ce que tu veux :";
	next;
	switch(select("^FF3355Reset Skills", "Reset Stats", "Reset les 2^000000", "Annuler")) {
	case 1:
		mes "[Resetteur]";
		if (Zeny < .@ResetSkill) {
			mes "Pardon, mais tu n'as pas assez de Zeny.";
			close;
		}
		Zeny -= .@ResetSkill;
		sc_end SC_ALL;
		resetskill;
		mes "Et voila !";
		close;
	case 2:
		mes "[Resetteur]";
		if (Zeny < .@ResetStat) {
			mes "Pardon, mais tu n'as pas assez de Zeny.";
			close;
		}
		Zeny -= .@ResetStat;
		resetstatus;
		mes "Et voila !";
		close;
	case 3:
		mes "[Resetteur]";
		if (Zeny < .@ResetBoth) {
			mes "Pardon, mais tu n'as pas assez de Zeny.";
			close;
		}
		Zeny -= .@ResetBoth;
		sc_end SC_ALL;
		resetskill;
		resetstatus;
		mes "Et voila !";
		close;
	case 4:
		close;
	}
}
